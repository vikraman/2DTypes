\AgdaHide{
\begin{code}
{-# OPTIONS --without-K #-}

module pifrac where

open import Level renaming (zero to l0)
open import Universe

open import Data.Product hiding (<_,_>)
open import Data.Nat
open import Data.Integer

open import Categories.Category
open import Categories.Groupoid

infix 40 _^_
infix 50 _âŠ•_
infix 60 _âŠ—_
infix  30 _âŸ·_
infix  30 _â‡”_
infixr 50 _â—_

\end{code}
}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{$\Pi^/$}

We are now ready to turn the semantic treatment of groupoids from the
previous section into an actual programming language. The language
$\Pi^/$ will be an extension of $\Pi$ with new type constructors and
new combinators for creating and manipulating fractional types. Every
computation in $\Pi^/$ will also be information preserving but with
the added expressiveness of being able to create and annihilate
negative information. We use Agda as the appropriate metalanguage in
which to define $\Pi^/$.

%%%%%%%%%%%
\subsection{Types and Combinators}

We begin by defining two mutually recursive syntactic categories
\AgdaRef{U} and \AgdaDatatype{âŸ·} of types and 1-combinators. The
definitions are identical to the presentation of $\Pi$ in
Sec.~\ref{sec:pi} except for the addition of the type constructors
\AgdaInductiveConstructor{\#} and \AgdaInductiveConstructor{1/\#} that
create order groupoids and inverse order groupoids respectively. We
will introduce additional combinators in proper time. 

{\setlength{\mathindent}{0cm}
\medskip
{\footnotesize{
\begin{code}
mutual
  
  -- Finite types (cf. Sec. 3.1) extended
  -- with #p and 1/#p 

  data U : Set where
    ğŸ˜    : U
    ğŸ™    : U
    _âŠ•_  : U â†’ U â†’ U
    _âŠ—_  : U â†’ U â†’ U
    #    : {Ï„ : U} â†’ (Ï„ âŸ· Ï„) â†’ U
    1/#  : {Ï„ : U} â†’ (Ï„ âŸ· Ï„) â†’ U

  -- Combinators (cf. Fig. 2)

  data PrimâŸ· : U â†’ U â†’ Set where
    -- additive monoid
    uniteâ‚Šl :   {t : U} â†’ PrimâŸ· (ğŸ˜ âŠ• t) t
    unitiâ‚Šl :   {t : U} â†’ PrimâŸ· t (ğŸ˜ âŠ• t)
    uniteâ‚Šr :   {t : U} â†’ PrimâŸ· (t âŠ• ğŸ˜) t
    unitiâ‚Šr :   {t : U} â†’ PrimâŸ· t (t âŠ• ğŸ˜)
    swapâ‚Š   :   {tâ‚ tâ‚‚ : U} â†’ PrimâŸ· (tâ‚ âŠ• tâ‚‚) (tâ‚‚ âŠ• tâ‚)
    assoclâ‚Š :   {tâ‚ tâ‚‚ tâ‚ƒ : U} â†’
                PrimâŸ· (tâ‚ âŠ• (tâ‚‚ âŠ• tâ‚ƒ))  ((tâ‚ âŠ• tâ‚‚) âŠ• tâ‚ƒ)
    assocrâ‚Š :   {tâ‚ tâ‚‚ tâ‚ƒ : U} â†’
                PrimâŸ· ((tâ‚ âŠ• tâ‚‚) âŠ• tâ‚ƒ) (tâ‚ âŠ• (tâ‚‚ âŠ• tâ‚ƒ))
    -- multiplicative monoid
    uniteâ‹†l :   {t : U} â†’ PrimâŸ· (ğŸ™ âŠ— t) t
    unitiâ‹†l :   {t : U} â†’ PrimâŸ· t (ğŸ™ âŠ— t)
    uniteâ‹†r :   {t : U} â†’ PrimâŸ· (t âŠ— ğŸ™) t
    unitiâ‹†r :   {t : U} â†’ PrimâŸ· t (t âŠ— ğŸ™)
    swapâ‹†   :   {tâ‚ tâ‚‚ : U} â†’ PrimâŸ· (tâ‚ âŠ— tâ‚‚) (tâ‚‚ âŠ— tâ‚)
    assoclâ‹† :   {tâ‚ tâ‚‚ tâ‚ƒ : U} â†’
                PrimâŸ· (tâ‚ âŠ— (tâ‚‚ âŠ— tâ‚ƒ)) ((tâ‚ âŠ— tâ‚‚) âŠ— tâ‚ƒ)
    assocrâ‹† :   {tâ‚ tâ‚‚ tâ‚ƒ : U} â†’
                PrimâŸ· ((tâ‚ âŠ— tâ‚‚) âŠ— tâ‚ƒ) (tâ‚ âŠ— (tâ‚‚ âŠ— tâ‚ƒ))
    -- multiplication distributes over addition
    absorbr :   {t : U} â†’ PrimâŸ· (ğŸ˜ âŠ— t) ğŸ˜
    absorbl :   {t : U} â†’ PrimâŸ· (t âŠ— ğŸ˜) ğŸ˜
    factorzr :  {t : U} â†’ PrimâŸ· ğŸ˜ (t âŠ— ğŸ˜)
    factorzl :  {t : U} â†’ PrimâŸ· ğŸ˜ (ğŸ˜ âŠ— t)
    dist :      {tâ‚ tâ‚‚ tâ‚ƒ : U} â†’
                PrimâŸ· ((tâ‚ âŠ• tâ‚‚) âŠ— tâ‚ƒ) ((tâ‚ âŠ— tâ‚ƒ) âŠ• (tâ‚‚ âŠ— tâ‚ƒ))
    factor :    {tâ‚ tâ‚‚ tâ‚ƒ : U} â†’
                PrimâŸ· ((tâ‚ âŠ— tâ‚ƒ) âŠ• (tâ‚‚ âŠ— tâ‚ƒ)) ((tâ‚ âŠ• tâ‚‚) âŠ— tâ‚ƒ)
    distl :     {tâ‚ tâ‚‚ tâ‚ƒ : U} â†’
                PrimâŸ· (tâ‚ âŠ— (tâ‚‚ âŠ• tâ‚ƒ)) ((tâ‚ âŠ— tâ‚‚) âŠ• (tâ‚ âŠ— tâ‚ƒ))
    factorl :   {tâ‚ tâ‚‚ tâ‚ƒ : U} â†’
                PrimâŸ· ((tâ‚ âŠ— tâ‚‚) âŠ• (tâ‚ âŠ— tâ‚ƒ)) (tâ‚ âŠ— (tâ‚‚ âŠ• tâ‚ƒ))

  data _âŸ·_ : U â†’ U â†’ Set where
    Prim :  {tâ‚ tâ‚‚ : U} â†’ (PrimâŸ· tâ‚ tâ‚‚) â†’ (tâ‚ âŸ· tâ‚‚)
    idâŸ· :   {t : U} â†’ t âŸ· t
    _â—_ :   {tâ‚ tâ‚‚ tâ‚ƒ : U} â†’ (tâ‚ âŸ· tâ‚‚) â†’ (tâ‚‚ âŸ· tâ‚ƒ) â†’ (tâ‚ âŸ· tâ‚ƒ)
    _âŠ•_ :   {tâ‚ tâ‚‚ tâ‚ƒ tâ‚„ : U} â†’
            (tâ‚ âŸ· tâ‚ƒ) â†’ (tâ‚‚ âŸ· tâ‚„) â†’ (tâ‚ âŠ• tâ‚‚ âŸ· tâ‚ƒ âŠ• tâ‚„)
    _âŠ—_ :   {tâ‚ tâ‚‚ tâ‚ƒ tâ‚„ : U} â†’
            (tâ‚ âŸ· tâ‚ƒ) â†’ (tâ‚‚ âŸ· tâ‚„) â†’ (tâ‚ âŠ— tâ‚‚ âŸ· tâ‚ƒ âŠ— tâ‚„)
\end{code}}}}

The complete code also includes definitions for \AgdaFunction{!} which
inverts a 1-combinator, \AgdaDatatype{â‡”} which defines equivalences of
1-combinators using 2-combinators, \AgdaFunction{2!} which inverts
2-combinators, and \AgdaFunction{!!â‡”id} and \AgdaFunction{â‡”!} which
show that 2-combinators commute as expected with inversion of
1-combinators:

{\setlength{\mathindent}{0cm}
\medskip
{\footnotesize{

\begin{code}
! : {tâ‚ tâ‚‚ : U} â†’ (tâ‚ âŸ· tâ‚‚) â†’ (tâ‚‚ âŸ· tâ‚)
\end{code}
\AgdaHide{
\begin{code}
! (Prim uniteâ‚Šl)   = Prim unitiâ‚Šl
! (Prim unitiâ‚Šl)   = Prim uniteâ‚Šl
! (Prim uniteâ‚Šr)   = Prim unitiâ‚Šr
! (Prim unitiâ‚Šr)   = Prim uniteâ‚Šr
! (Prim swapâ‚Š)     = Prim swapâ‚Š
! (Prim assoclâ‚Š)   = Prim assocrâ‚Š
! (Prim assocrâ‚Š)   = Prim assoclâ‚Š
! (Prim uniteâ‹†l)   = Prim unitiâ‹†l
! (Prim unitiâ‹†l)   = Prim uniteâ‹†l
! (Prim uniteâ‹†r)   = Prim unitiâ‹†r
! (Prim unitiâ‹†r)   = Prim uniteâ‹†r
! (Prim swapâ‹†)     = Prim swapâ‹†
! (Prim assoclâ‹†)   = Prim assocrâ‹†
! (Prim assocrâ‹†)   = Prim assoclâ‹†
! (Prim absorbl)   = Prim factorzr
! (Prim absorbr)   = Prim factorzl
! (Prim factorzl)  = Prim absorbr
! (Prim factorzr)  = Prim absorbl
! (Prim dist)      = Prim factor
! (Prim factor)    = Prim dist
! (Prim distl)     = Prim factorl
! (Prim factorl)   = Prim distl
! idâŸ·       = idâŸ·
! (câ‚ â— câ‚‚) = ! câ‚‚ â— ! câ‚
! (câ‚ âŠ• câ‚‚) = (! câ‚) âŠ• (! câ‚‚)
! (câ‚ âŠ— câ‚‚) = (! câ‚) âŠ— (! câ‚‚)
\end{code}}

\medskip
\begin{code}
data _â‡”_ : {tâ‚ tâ‚‚ : U} â†’ (tâ‚ âŸ· tâ‚‚) â†’ (tâ‚ âŸ· tâ‚‚) â†’ Set
\end{code}
\AgdaHide{
\begin{code}
  where
  assocâ—l : âˆ€ {tâ‚ tâ‚‚ tâ‚ƒ tâ‚„} {câ‚ : tâ‚ âŸ· tâ‚‚} {câ‚‚ : tâ‚‚ âŸ· tâ‚ƒ} {câ‚ƒ : tâ‚ƒ âŸ· tâ‚„} â†’
    (câ‚ â— (câ‚‚ â— câ‚ƒ)) â‡” ((câ‚ â— câ‚‚) â— câ‚ƒ)
  assocâ—r : âˆ€ {tâ‚ tâ‚‚ tâ‚ƒ tâ‚„} {câ‚ : tâ‚ âŸ· tâ‚‚} {câ‚‚ : tâ‚‚ âŸ· tâ‚ƒ} {câ‚ƒ : tâ‚ƒ âŸ· tâ‚„} â†’
    ((câ‚ â— câ‚‚) â— câ‚ƒ) â‡” (câ‚ â— (câ‚‚ â— câ‚ƒ))
  idlâ—l   : âˆ€ {tâ‚ tâ‚‚} {c : tâ‚ âŸ· tâ‚‚} â†’
    (idâŸ· â— c) â‡” c
  idlâ—r   : âˆ€ {tâ‚ tâ‚‚} {c : tâ‚ âŸ· tâ‚‚} â†’
    c â‡” idâŸ· â— c
  idrâ—l   : âˆ€ {tâ‚ tâ‚‚} {c : tâ‚ âŸ· tâ‚‚} â†’
    (c â— idâŸ·) â‡” c
  idrâ—r   : âˆ€ {tâ‚ tâ‚‚} {c : tâ‚ âŸ· tâ‚‚} â†’
    c â‡” (c â— idâŸ·)
  idâ‡”     : âˆ€ {tâ‚ tâ‚‚} {c : tâ‚ âŸ· tâ‚‚} â†’
    c â‡” c
  rinvâ—l  : {tâ‚ tâ‚‚ : U} {c : tâ‚ âŸ· tâ‚‚} â†’ (! c â— c) â‡” idâŸ·
  rinvâ—r  : {tâ‚ tâ‚‚ : U} {c : tâ‚ âŸ· tâ‚‚} â†’ idâŸ· â‡” (! c â— c)
  linvâ—l  : {tâ‚ tâ‚‚ : U} {c : tâ‚ âŸ· tâ‚‚} â†’ (c â— ! c) â‡” idâŸ·
  linvâ—r  : {tâ‚ tâ‚‚ : U} {c : tâ‚ âŸ· tâ‚‚} â†’ idâŸ· â‡” (c â— ! c)
  transâ‡”  : âˆ€ {tâ‚ tâ‚‚} {câ‚ câ‚‚ câ‚ƒ : tâ‚ âŸ· tâ‚‚} â†’
    (câ‚ â‡” câ‚‚) â†’ (câ‚‚ â‡” câ‚ƒ) â†’ (câ‚ â‡” câ‚ƒ)
  _âŠ¡_  : âˆ€ {tâ‚ tâ‚‚ tâ‚ƒ} {câ‚ câ‚ƒ : tâ‚ âŸ· tâ‚‚} {câ‚‚ câ‚„ : tâ‚‚ âŸ· tâ‚ƒ} â†’
    (câ‚ â‡” câ‚ƒ) â†’ (câ‚‚ â‡” câ‚„) â†’ (câ‚ â— câ‚‚) â‡” (câ‚ƒ â— câ‚„)
  respâŠ•â‡”  : {tâ‚ tâ‚‚ tâ‚ƒ tâ‚„ : U}
         {câ‚ : tâ‚ âŸ· tâ‚‚} {câ‚‚ : tâ‚ƒ âŸ· tâ‚„} {câ‚ƒ : tâ‚ âŸ· tâ‚‚} {câ‚„ : tâ‚ƒ âŸ· tâ‚„} â†’
         (câ‚ â‡” câ‚ƒ) â†’ (câ‚‚ â‡” câ‚„) â†’ (câ‚ âŠ• câ‚‚) â‡” (câ‚ƒ âŠ• câ‚„)
  respâŠ—â‡”  : {tâ‚ tâ‚‚ tâ‚ƒ tâ‚„ : U}
         {câ‚ : tâ‚ âŸ· tâ‚‚} {câ‚‚ : tâ‚ƒ âŸ· tâ‚„} {câ‚ƒ : tâ‚ âŸ· tâ‚‚} {câ‚„ : tâ‚ƒ âŸ· tâ‚„} â†’
         (câ‚ â‡” câ‚ƒ) â†’ (câ‚‚ â‡” câ‚„) â†’ (câ‚ âŠ— câ‚‚) â‡” (câ‚ƒ âŠ— câ‚„)
  homâŠ•â—â‡” : {tâ‚ tâ‚‚ tâ‚ƒ tâ‚„ tâ‚… tâ‚† : U} {câ‚ : tâ‚… âŸ· tâ‚} {câ‚‚ : tâ‚† âŸ· tâ‚‚}
        {câ‚ƒ : tâ‚ âŸ· tâ‚ƒ} {câ‚„ : tâ‚‚ âŸ· tâ‚„} â†’
        ((câ‚ â— câ‚ƒ) âŠ• (câ‚‚ â— câ‚„)) â‡” ((câ‚ âŠ• câ‚‚) â— (câ‚ƒ âŠ• câ‚„))
  homâ—âŠ•â‡” : {tâ‚ tâ‚‚ tâ‚ƒ tâ‚„ tâ‚… tâ‚† : U} {câ‚ : tâ‚… âŸ· tâ‚} {câ‚‚ : tâ‚† âŸ· tâ‚‚}
        {câ‚ƒ : tâ‚ âŸ· tâ‚ƒ} {câ‚„ : tâ‚‚ âŸ· tâ‚„} â†’
         ((câ‚ âŠ• câ‚‚) â— (câ‚ƒ âŠ• câ‚„)) â‡” ((câ‚ â— câ‚ƒ) âŠ• (câ‚‚ â— câ‚„))
  splitâŠ•-idâŸ· : {tâ‚ tâ‚‚ : U} â†’ idâŸ· {tâ‚ âŠ• tâ‚‚} â‡” idâŸ· âŠ• idâŸ·
  idâŸ·âŠ•idâŸ·â‡” : {tâ‚ tâ‚‚ : U} â†’ (idâŸ· {tâ‚} âŠ• idâŸ· {tâ‚‚}) â‡” idâŸ·
\end{code}}

\medskip
\begin{code}
2! : {tâ‚ tâ‚‚ : U} {câ‚ câ‚‚ : tâ‚ âŸ· tâ‚‚} â†’ (câ‚ â‡” câ‚‚) â†’ (câ‚‚ â‡” câ‚)
\end{code}
\AgdaHide{
\begin{code}
2! assocâ—l = assocâ—r
2! assocâ—r = assocâ—l
2! idlâ—l = idlâ—r
2! idlâ—r = idlâ—l
2! idrâ—l = idrâ—r
2! idrâ—r = idrâ—l
2! rinvâ—l = rinvâ—r
2! rinvâ—r = rinvâ—l
2! linvâ—l = linvâ—r
2! linvâ—r = linvâ—l
2! idâ‡” = idâ‡”
2! (Î± âŠ¡ Î²) = (2! Î±) âŠ¡ (2! Î²)
2! (transâ‡” Î± Î²) = transâ‡” (2! Î²) (2! Î±)
2! (respâŠ•â‡” Î± Î²) = respâŠ•â‡” (2! Î±) (2! Î²)
2! (respâŠ—â‡” Î± Î²) = respâŠ—â‡” (2! Î±) (2! Î²)
2! homâŠ•â—â‡” = homâ—âŠ•â‡”
2! homâ—âŠ•â‡” = homâŠ•â—â‡”
2! splitâŠ•-idâŸ· = idâŸ·âŠ•idâŸ·â‡”
2! idâŸ·âŠ•idâŸ·â‡” = splitâŠ•-idâŸ· 

!!â‡”prim : {tâ‚ tâ‚‚ : U} â†’ (p : PrimâŸ· tâ‚ tâ‚‚) â†’ Prim p â‡” (! (! (Prim p)))
!!â‡”prim uniteâ‚Šl = idâ‡”
!!â‡”prim unitiâ‚Šl = idâ‡”
!!â‡”prim uniteâ‚Šr = idâ‡”
!!â‡”prim unitiâ‚Šr = idâ‡”
!!â‡”prim swapâ‚Š = idâ‡”
!!â‡”prim assoclâ‚Š = idâ‡”
!!â‡”prim assocrâ‚Š = idâ‡”
!!â‡”prim uniteâ‹†l = idâ‡”
!!â‡”prim unitiâ‹†l = idâ‡”
!!â‡”prim uniteâ‹†r = idâ‡”
!!â‡”prim unitiâ‹†r = idâ‡”
!!â‡”prim swapâ‹† = idâ‡”
!!â‡”prim assoclâ‹† = idâ‡”
!!â‡”prim assocrâ‹† = idâ‡”
!!â‡”prim absorbr = idâ‡”
!!â‡”prim absorbl = idâ‡”
!!â‡”prim factorzr = idâ‡”
!!â‡”prim factorzl = idâ‡”
!!â‡”prim dist = idâ‡”
!!â‡”prim factor = idâ‡”
!!â‡”prim distl = idâ‡”
!!â‡”prim factorl = idâ‡”
\end{code}}

\medskip
\begin{code}
!!â‡”id : {tâ‚ tâ‚‚ : U} â†’ (p : tâ‚ âŸ· tâ‚‚) â†’ p â‡” ! (! p)
\end{code}
\AgdaHide{
\begin{code}
!!â‡”id idâŸ· = idâ‡”
!!â‡”id (_âŸ·_.Prim c) = !!â‡”prim c
!!â‡”id (p â— q) = !!â‡”id p âŠ¡ !!â‡”id q
!!â‡”id (p _âŸ·_.âŠ• q) = respâŠ•â‡” (!!â‡”id p) (!!â‡”id q)
!!â‡”id (p _âŸ·_.âŠ— q) = respâŠ—â‡” (!!â‡”id p) (!!â‡”id q)
\end{code}}

\medskip
\begin{code}
â‡”! : {Ï„â‚ Ï„â‚‚ : U} {p q : Ï„â‚ âŸ· Ï„â‚‚} â†’ (p â‡” q) â†’ (! p â‡” ! q)
\end{code}
\AgdaHide{
\begin{code}
â‡”! assocâ—l = assocâ—r
â‡”! assocâ—r = assocâ—l
â‡”! idlâ—l = idrâ—l
â‡”! idlâ—r = idrâ—r
â‡”! idrâ—l = idlâ—l
â‡”! idrâ—r = idlâ—r
â‡”! idâ‡” = idâ‡”
â‡”! rinvâ—l = linvâ—l
â‡”! rinvâ—r = linvâ—r
â‡”! linvâ—l = rinvâ—l
â‡”! linvâ—r = rinvâ—r
â‡”! (transâ‡” qâ‚ qâ‚‚) = transâ‡” (â‡”! qâ‚) (â‡”! qâ‚‚)
â‡”! (qâ‚ âŠ¡ qâ‚‚) = â‡”! qâ‚‚ âŠ¡ â‡”! qâ‚
â‡”! (respâŠ•â‡” qâ‚ qâ‚‚) = respâŠ•â‡” (â‡”! qâ‚) (â‡”! qâ‚‚)
â‡”! (respâŠ—â‡” qâ‚ qâ‚‚) = respâŠ—â‡” (â‡”! qâ‚) (â‡”! qâ‚‚)
â‡”! homâŠ•â—â‡” = homâŠ•â—â‡”
â‡”! homâ—âŠ•â‡” = homâ—âŠ•â‡”
â‡”! splitâŠ•-idâŸ· = splitâŠ•-idâŸ· 
â‡”! idâŸ·âŠ•idâŸ·â‡” = idâŸ·âŠ•idâŸ·â‡”
\end{code}}}}}

As motivated in the previous section, we will also need to consider
the iterates $p^k$ of combinators $p$ which are $k$-fold compositions
of $p$ and its inverse. These iterates are not independent: there are
only $\ord{p}$ distinct iterates, up to 2-combinator equivalence:

{\setlength{\mathindent}{0cm}
\medskip
{\footnotesize{
\begin{code}
_^_ : {Ï„ : U} â†’ (p : Ï„ âŸ· Ï„) â†’ (k : â„¤) â†’ (Ï„ âŸ· Ï„)
p ^ (+ 0)             = idâŸ·
p ^ (+ (suc k))       = p â— (p ^ (+ k))
p ^ -[1+ 0 ]          = ! p
p ^ (-[1+ (suc k) ])  = (! p) â— (p ^ -[1+ k ])

record Iter {Ï„ : U} (p : Ï„ âŸ· Ï„) : Set where
  constructor <_,_,_>
  field
    k : â„¤
    q : Ï„ âŸ· Ï„
    Î± : q â‡” p ^ k
\end{code}}}}  

For our running example using the type $\mathbb{3}$ and the combinator
$a_2$, we can a few iterates of $a_2$ as follows:

{\setlength{\mathindent}{0cm}
\medskip
{\footnotesize{
\begin{code}
ğŸ› : U
ğŸ› = (ğŸ™ âŠ• ğŸ™) âŠ• ğŸ™

aâ‚‚ : ğŸ› âŸ· ğŸ›
aâ‚‚ = Prim swapâ‚Š âŠ• idâŸ· 

p^â‚€ p^â‚ p^â‚‚ p^â‚ƒ p^â‚„ p^â‚… : Iter aâ‚‚
p^â‚€ = < + 0 , idâŸ· , idâ‡” > 
p^â‚ = < + 0 , idâŸ· â— idâŸ· , idrâ—l > 
p^â‚‚ = <  -[1+ 1 ] ,
         idâŸ· , 
         transâ‡” splitâŠ•-idâŸ·
         (transâ‡” (respâŠ•â‡” (linvâ—r {c = Prim swapâ‚Š}) idrâ—r)
         (transâ‡” homâŠ•â—â‡” idâ‡”)) >
p^â‚ƒ = <  + 2 ,
         idâŸ· ,
         transâ‡” splitâŠ•-idâŸ·
         (transâ‡” (respâŠ•â‡” (linvâ—r {c = Prim swapâ‚Š}) idrâ—r)
         (transâ‡” homâŠ•â—â‡” (idâ‡” âŠ¡ idrâ—r))) >
p^â‚„ = < -[1+ 0 ] , aâ‚‚ , idâ‡” > 
p^â‚… = < + 1 , aâ‚‚ , idrâ—r > 
\end{code}}}}  

\noindent Since $a_2$ has order 2, there are only two distinguishable
iterates. The first four iterates are all equivalent to $(a_2)^0$
which is equivalent \AgdaInductiveConstructor{idâŸ·}. The last two are
both equivalent to $(a_2)^1$ which is equivalent to $a_2$. The
equivalences are explicit in the construction. 

%%%%%%%%%%%
\subsection{Values}

When the types denote sets, it is evident what it means to have a
value of a given type: it is just an element of the set. When types
denote groupoids, it is less clear what it means to have a value,
especially when the total number of values, as reported by the
groupoid cardinality, is a proper fraction. We obviously cannot list
``half a value'' but what we \emph{can} do is to list an integral
number of values and provide an equivalence relation that specifies
which values are distinguishable such that the ultimate counting of
distinguishable values is a fractional amount. The idea is not
uncommon: in the conventional $\lambda$-calculus, we list $\lambda
x.x$ and $\lambda y.y$ as separate values of type $\tau \rightarrow
\tau$ and then provide a separate equivalence relation
($\alpha$-equivalence) to express the fact that these two values are
indistinguishable. The treatment in our setting is similar but richer
as the equivalence relation is not external but is itself part of the
value and the resulting count may be fractional. Formally we define
values as follows:

{\setlength{\mathindent}{0cm}
\medskip
{\footnotesize{
\begin{code}
data Val : (Ï„ : U) â†’ Set where
  â‹† :       Val ğŸ™
  inl :     {Ï„â‚ Ï„â‚‚ : U} â†’ Val Ï„â‚ â†’ Val (Ï„â‚ âŠ• Ï„â‚‚)
  inr :     {Ï„â‚ Ï„â‚‚ : U} â†’ Val Ï„â‚‚ â†’ Val (Ï„â‚ âŠ• Ï„â‚‚)
  [_,_] :   {Ï„â‚ Ï„â‚‚ : U} â†’ Val Ï„â‚ â†’ Val Ï„â‚‚ â†’ Val (Ï„â‚ âŠ— Ï„â‚‚)
  comb :    {Ï„ : U} {p : Ï„ âŸ· Ï„} â†’ (páµ : Iter p) â†’  Val (# p)
  1/comb :  {Ï„ : U} {p : Ï„ âŸ· Ï„} â†’ (páµ : Iter p) â†’ Val (1/# p)
\end{code}}}}

\noindent The first four lines define the conventional values for the
unit, sum, and product types. The last two lines define values of type
$\order{p}$ and $\iorder{p}$ as iterates of $p$. In the case of
$\order{p}$ the iterates are interpreted as ``\emph{programs}'' that
can act on other values and in the case of $\iorder{p}$ the iterates
are interpreted as ``\emph{symmetries}'' that capture similarities in
values. Note that if $p$ has order, say 3, then there are 3 distinct
values of type $\order{p}$ and 3 distinct values of $\iorder{p}$. The
values of type $\order{p}$ apply $p$ for 0, 1, or 2 times to given
value. The values of type $\iorder{p}$ represent the three ``thirds''
of $p$. If the full action of applying $p$ to a value is thought of as
a rotation for example, then applying each third would correspond to
1/3 of a rotation. 

%%%%%%%
\subsection{Additional Combinators}

most combinators do not look at higher components of values:
indistinguishable values are treated the same!

Our aim is to ensure that $G_1$, $G_2$, and $G_3$ are the denotations
of types with $\frac{3}{2}$ values and that the values of these types
are in 1-1 correspondence. 

\begin{definition}[Semantic Values] Given a groupoid $G$, a
  \emph{value} in~$G$ is a pair consisting of an object $v$ and its
  collection $[\alpha_i]_i$ of outgoing morphisms
  $\alpha_i : v \Rightarrow w_i$ for each reachable object $w_i$.
\end{definition}

\noindent The values in each of our three example groupoids in Fig.~\ref{fig:groupoids2} are:
\begin{itemize}
\item Values of $G_1$ are $(a,[\texttt{id}])$ and $(c,[\texttt{id},\swapp])$;
\item Values of $G_2$ are $(a,[\texttt{id},\swapp])$, $(b,[\texttt{id},\swapp])$, and \\
$(c, [\texttt{id}, \swapp])$; 
\item Values of $G_3$ are $(a,[\texttt{id},\swapp])$, $(b,[\texttt{id},\swapp])$, and \\
$(c, [\texttt{id}, \swapp])$.
\end{itemize}

These values can be considered raw values as they are not all
indistinguishable. But we already see that $G_2$ and $G_3$ have the
same raw values and hence can be reasonably considered as
equivalent. To argue that either is also equivalent to $G_1$, we
reason following a monetary analogy: why is a dollar bill (the value
$(a,[\texttt{id}])$ in $G_1$) considered indistinguishable from two
half-dollars (the values $(a,[\texttt{id},\swapp])$ and
$(b,[\texttt{id},\swapp])$ in $G_2$)? Certainly not because we can
find a 1-1 map between one object and two objects but because we have
a process that can transform one collection of values to the other. In
our case, we proceed as follows. Consider the value
$(a,[\texttt{id}])$ in $G_1$; we can add another copy $\texttt{id}$
and refine it to $\swapp\odot\swapp$ to transform the value to
$(a,[\texttt{id},\swapp\odot\swapp])$. We then introduce a new
intermediate object so that the loop $\swapp\odot\swapp])$ from $a$ to
$a$ goes through an intermediate point $b$, i.e., we have a path
$\swapp$ from $a$ to $b$ and a path $\swapp$ from $b$ to $a$. The
result is groupoid $G_2$.

\begin{definition}[Distinguishability] Two \emph{collections of
    values} $\{V_i\}$ and $\{W_j\}$ in $G$ are indistinguishable
  $\{V_i\} \sim \{W_j\}$, if \ldots
  morphisms.
\end{definition}

\amr{formalize the above and then revisit G1, G2, and G3 to formally
  argue that after taking distinguishability into account they all
  have the same distinguishable values and the number of
  distinguishable values is 1.5}

combinators between FT/ types including eta and epsilon

proof that combinators are information preserving

other properties: inverses etc.

Cardinality-preserving combinators: sound, not complete (see
limitations section), consistent.

Consistency is defined in the following sense: If we allow arbitrary
functions then bad things happen as we can throw away the negative
information for example. In our reversible information-preserving
framework, the theory is consistent in the sense that not all types
are identified. This is easy to see as we only identify types that
have the same cardinality. This is evident for all the combinators
except for the new ones. For those new ones the only subtle situation
is with the empty type. Note however that there is no way to define
1/0 and no permutation has order 0. For 0 we have one permutation id
which has order 1. So if we try to use it, we will map 1 to 1 times
1/id which is fine. So if we always preserve types and trivially 1 and
0 have different cardinalities so there is no way to identify them and
we are consistent.

values equivalent tilde tilde

values indistinguishable

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


